buildscript {
    ext.kotlinVersion = '1.0.3'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.9.1'
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: "${kotlinVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'kotlin'
apply plugin: 'idea'

group = 'org.jetbrains.teamcity.github'

if (PluginVersion == null || "" == PluginVersion) {
    version = TeamCityVersion
} else {
    version = "$PluginVersion"
}

ext {
    teamcityDir = "$rootDir/servers/TeamCity-${TeamCityVersion}"
    teamcityDataDir = "$rootDir/data/" + (
            (TeamCityVersion ==~ /(\d+\.\d+).*/) ?
            ((TeamCityVersion =~ /(\d+\.\d+).*/)[0][1])
            : TeamCityVersion
    )
    teamcityJavaHome = System.properties['java.home']
}

// Remove repositories added by plugins
project.plugins.withType(JavaPlugin) {
    project.repositories.clear()
}
repositories {
    maven { url "http://download.jetbrains.com/teamcity-repository" }
    mavenCentral()
}

configurations {
}

dependencies {
    provided('org.eclipse.mylyn.github:org.eclipse.egit.github.core:4.1.0.201509280440-r') {
        transitive = false
    }
    provided(group: 'org.jetbrains.teamcity.internal', name: 'server', version: "${TeamCityVersion}")
    provided(group: 'org.jetbrains.teamcity', name: 'oauth', version: "${TeamCityVersion}")
    provided(group: 'org.jetbrains.teamcity', name: 'server-web-api', version: "${TeamCityVersion}")
    compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")

    testCompile 'org.assertj:assertj-core:2.2.0'
}

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

test {
    useTestNG()
}

teamcity {
    version = TeamCityVersion

    server {
        descriptor {
            name = project.name
            displayName = 'GitHub Webhooks'
            version = project.version
            vendorName = 'JetBrains, s.r.o.'
            vendorUrl = 'http://www.jetbrains.com/'
            description = 'Allows to install GitHub Webhooks pointing to TeamCity VCS roots'

            useSeparateClassloader = true
        }

        environments {
            teamcityDev {
                version = TeamCityVersion
                homeDir = file(teamcityDir)
                dataDir = file(teamcityDataDir)
                javaHome = file(teamcityJavaHome)
                serverOptions '-Xdebug'
                serverOptions '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5500'

                serverOptions '-Dteamcity.oauthConnections.enabled=true'

                serverOptions '-Dteamcity.development.mode=true'
                serverOptions '-Dteamcity.development.shadowCopyClasses=true'
            }

        }
    }

}

// Do not include version into plugin archive name
project.tasks.getByName('serverPlugin').version = ''

idea {
    module {
        downloadJavadoc = false
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
